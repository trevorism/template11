buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        maven {
            url uri("https://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
        }
    }
    dependencies {
        classpath 'com.google.cloud.tools:appengine-gradle-plugin:2.4.1'
        classpath 'com.trevorism:gradle-acceptance-plugin:2.0.0'
        classpath 'com.trevorism:gradle-release-plugin:0.4.0'
    }
}

plugins {
    id "groovy"
    id "jacoco"
}

apply plugin: 'com.google.cloud.tools.appengine'
apply plugin: "com.trevorism.gradle.acceptance"
apply plugin: "com.trevorism.gradle.release"

repositories {
    mavenCentral()
    maven {
        url uri("https://trevorism-build.eastus.cloudapp.azure.com/nexus/repository/maven-releases")
    }
}

def jerseyVersion = "3.0.1"

dependencies {
    implementation 'org.codehaus.groovy:groovy-all:3.0.7'

    implementation group: 'org.eclipse.jetty', name: 'jetty-server', version: '11.0.2'
    implementation group: 'org.eclipse.jetty', name: 'jetty-servlet', version: '11.0.2'

    implementation "org.glassfish.jersey.containers:jersey-container-servlet:$jerseyVersion"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:$jerseyVersion"
    implementation "org.glassfish.jersey.inject:jersey-hk2:$jerseyVersion"
    implementation 'com.fasterxml.jackson.jaxrs:jackson-jaxrs-json-provider:2.12.3'

    implementation group: 'jakarta.xml.bind', name: 'jakarta.xml.bind-api', version: '3.0.1'
    implementation group: 'org.glassfish.jaxb', name: 'jaxb-runtime', version: '3.0.1'

    implementation 'org.slf4j:slf4j-nop:2.0.0-alpha1'
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2-jakarta', version: '2.1.9'
    implementation group: 'io.swagger.core.v3', name: 'swagger-annotations', version: '2.1.9'
    implementation group: 'io.swagger.core.v3', name: 'swagger-jaxrs2-servlet-initializer-v2', version: '2.1.9'

    acceptanceImplementation 'io.cucumber:cucumber-groovy:4.7.1'

    testImplementation 'junit:junit:4.13.2'
}


appengine {
    deploy {
        projectId = "trevorism-gcloud"
        stopPreviousVersion = true
        promote = true
        version = "0-0-1"
    }
}

check {
    dependsOn('jacocoTestReport')
}

task deliver {
    dependsOn("build","appengineDeploy")
    appengineDeploy.mustRunAfter("check")
}

jar {
    manifest {
        attributes 'Main-Class': 'com.trevorism.gcloud.Startup'
    }

    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}